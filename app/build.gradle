import java.nio.charset.Charset

apply plugin: 'com.android.application'
apply plugin: 'com.jakewharton.butterknife'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

File propFile = new File("E:/资料/jks/autojs-app/sign.properties")
Properties properties = new Properties()
if (propFile.exists()) {
    properties.load(new InputStreamReader(new FileInputStream(propFile), Charset.forName("utf-8")))
}

android {
    compileSdkVersion versions.compile
    buildToolsVersion versions.buildTool
    defaultConfig {
        applicationId "org.autojs.autojs"
        minSdkVersion versions.mini
        targetSdkVersion versions.target
        versionCode versions.appVersionCode
        versionName versions.appVersionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        'resourcePackageName': applicationId,
                        "androidManifestFile": "$projectDir/src/main/AndroidManifest.xml".toString()
                ]
            }
        }
    }
    signingConfigs {
        if (propFile.exists()) {
            release {
                keyAlias properties['keyAlias']
                keyPassword properties['keyPassword']
                storeFile file(properties['storeFile'])
                storePassword properties['storePassword']
            }
        }
    }
    buildTypes {
        debug {
            shrinkResources false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (propFile.exists()) {
                signingConfig signingConfigs.release
            }
        }
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (propFile.exists()) {
                signingConfig signingConfigs.release
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
        encoding = 'utf-8'
    }


    configurations.all {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:3.0.2'
    }

    flavorDimensions "channel"
    splits {

        // Configures multiple APKs based on ABI.
        abi {

            // Enables building multiple APKs per ABI.
            enable true

            // By default all ABIs are included, so use reset() and include to specify that we only
            // want APKs for x86 and x86_64.

            // Resets the list of ABIs that Gradle should create APKs for to none.
            reset()

            // Specifies a list of ABIs that Gradle should create APKs for.
            include "armeabi-v7a", "arm64-v8a"

            // Specifies that we do not want to also generate a universal APK that includes all ABIs.
            universalApk false
        }
    }
    productFlavors {
        common {
            versionCode versions.appVersionCode
            versionName versions.appVersionName
            buildConfigField "String", "CHANNEL", '"common"'
            buildConfigField "String", "APPID", '"?id=21"'
            manifestPlaceholders = [appName: "@string/app_name"]
        }
        dev {
            applicationIdSuffix ".dev"
            versionCode versions.devVersionCode
            versionName versions.devVersionName
            buildConfigField "String", "CHANNEL", '"dev"'
            buildConfigField "String", "APPID", '"?id=23"'
            manifestPlaceholders = [appName: "Autox.js[dev]"]
        }

    }

}
task publishApp(group: "build") {
    println "publish app"
    println "$rootDir"
    doLast {
        exec {
            //切换到sh文件所在的目录，执行脚本
            workingDir "E:/资料/jks/shell"
            //修改命令输出的地方，默认为控制台
            standardOutput = out
            //执行shell脚本
            if (System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows')) {
                executable 'cmd'
                args '/c', "node scnode.js $projectDir\\build\\outputs\\apk\\common\\release $rootDir 21"
            } else {
                commandLine 'sh', '-c', "node scnode.js $projectDir\\build\\outputs\\apk\\common\\release $rootDir 21"
            }
        }
    }
}
task publishDevApp(group: "build") {
    println "publish dev app"
    println "$rootDir"
    doLast {
        exec {
            //切换到sh文件所在的目录，执行脚本
            workingDir "E:/资料/jks/shell"
            //修改命令输出的地方，默认为控制台
            standardOutput = out
            //执行shell脚本
            if (System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows')) {
                executable 'cmd'
                args '/c', "node scnode.js $projectDir\\build\\outputs\\apk\\dev\\release $rootDir 23"
            } else {
                commandLine 'sh', '-c', "node scnode.js $projectDir\\build\\outputs\\apk\\dev\\release $rootDir 23"
            }
        }
    }
}

repositories {
    flatDir {
        dirs 'libs'
    }
    google()
}

dependencies {
    androidTestImplementation('androidx.test.espresso:espresso-core:3.4.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testImplementation 'junit:junit:4.13.2'
    // Kotlin
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.0-native-mt"
    // Android Annotations
    annotationProcessor "org.androidannotations:androidannotations:4.8.0"
    kapt "org.androidannotations:androidannotations:4.8.0"
    implementation "org.androidannotations:androidannotations-api:4.8.0"
    // ButterKnife
    implementation('com.jakewharton:butterknife:10.2.3', {
        exclude group: 'com.android.support'
    })
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.2.3'
    kapt 'com.jakewharton:butterknife-compiler:10.2.3'
    // Android supports
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.google.android.material:material:1.6.0-alpha01'
    // Personal libraries
    implementation 'com.github.hyb1996:MutableTheme:1.0.0'
    // Material Dialogs
    implementation('com.afollestad.material-dialogs:core:0.9.6.0', {
        exclude group: 'com.android.support'
    })
    // Common Markdown
    implementation 'com.github.atlassian:commonmark-java:commonmark-parent-0.15.2'
    // Android issue reporter (a github issue reporter)
    implementation('com.heinrichreimersoftware:android-issue-reporter:1.3.1', {
        exclude group: 'com.afollestad.material-dialogs'
        exclude group: 'com.android.support'
    })
    //MultiLevelListView
    implementation 'com.github.hyb1996:android-multi-level-listview:1.1'
    //Licenses Dialog
    implementation 'de.psdev.licensesdialog:licensesdialog:1.8.0'
    //Expandable RecyclerView
    implementation 'com.bignerdranch.android:expandablerecyclerview:3.0.0-RC1'
    //FlexibleDivider
    implementation 'com.yqritc:recyclerview-flexibledivider:1.4.0'
    //???
    implementation 'com.wang.avi:library:2.1.3'
    //Commons-lang
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    // 证书签名相关
    implementation 'com.madgag.spongycastle:bcpkix-jdk15on:1.58.0.0'
    //Expandable RecyclerView
    implementation 'com.thoughtbot:expandablerecyclerview:1.4'
    // implementation 'org.signal.autox:apkbuilder:1.0.3'
    // RxJava
    implementation "io.reactivex.rxjava3:rxjava:3.1.3"
    implementation 'io.reactivex.rxjava3:rxandroid:3.0.0'
    // Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava3:2.9.0'
    //Glide
    implementation('com.github.bumptech.glide:glide:4.12.0', {
        exclude group: 'com.android.support'
    })
    kapt 'com.github.bumptech.glide:compiler:4.12.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.12.0'
    //joda time
    implementation 'joda-time:joda-time:2.10.14'
    // Tasker Plugin
    implementation 'com.twofortyfouram:android-plugin-client-sdk-for-locale:4.0.3'
    // MaterialDialogCommon
    implementation('com.afollestad.material-dialogs:commons:0.9.6.0', {
        exclude group: 'com.android.support'
    })
    // Android job
    implementation 'com.evernote:android-job:1.4.2'
    implementation project(':automator')
    implementation project(':common')
    implementation project(':autojs')
    implementation project(':apkbuilder')
}
